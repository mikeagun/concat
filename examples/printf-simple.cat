#Copyright (C) 2024 D. Michael Agun
#
#Licensed under the Apache License, Version 2.0 (the "License");
#you may not use this file except in compliance with the License.
#You may obtain a copy of the License at
#
#http://www.apache.org/licenses/LICENSE-2.0
#
#Unless required by applicable law or agreed to in writing, software
#distributed under the License is distributed on an "AS IS" BASIS,
#WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#See the License for the specific language governing permissions and
#limitations under the License.

#simple printf implementation in concat
# - Note that concat has a complete printf implementation already, this is an example of implementing one from scratch
# - FIXME: this is from old implementation needs updating to match new impl
#
#Supported escapes
#  - %%  - literal %
#  - %v  - print
#  - %V  - print code
#  - %_  - pop top of stack
#
[
  [ dup '%' find dup 0 ge ] [
    splitn swap print_ #split string and print literal prefix
    2 splitn swap second #split off next 2 chars, keep just char after escape
    dup
    "%vV_" swap find inc dup [
      ( [ print_ ] #%%
        [ pop \print_ dip ] #%v
        [ pop \print_code_ dip ] #%V
        [ pop popd ] #%_
      ) swap nth eval
    ] [ pop #pop -1
      '%' print_ print_
    ] ifelse
  ] while
  pop #pop final index from find (which must be -1)
  print_ #| print tail of the string
] \printf def #| ...args "formatstring" --


#example usage:
# "a" 5 "hello" "%v world. x=%v and s=%V" printf #should see: hello world. x=5 and s="a"
